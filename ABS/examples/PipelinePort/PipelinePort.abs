/*************************************
Tweet example, inspired by the scalalogy pipeline example
FIRST VERSION BY(2021.04.24): S. Lizeth Tapia Tarifa
************************************/


module PipelinePort;
export *;

import * from Medium;
import * from Event;
import * from Stream;
import * from Port;

interface InputI extends Tier {
  Unit fire(Event t);
}

class Input() implements InputI {
  PortI portTo = null;

  Unit connectToPort(PortI p){portTo = p;}
  Unit connectFromPort(PortI p){skip;}

  Unit fire(Event tweet) {
    await portTo != null;
    portTo!fire(this,tweet);
  //  println(`Input has sent $t$`);
  }
}


interface FilterI extends Tier {
  Unit filtered(Hashtag ht);
}

class Filter() implements FilterI {
  PortI portFrom = null;
  PortI portTo = null;

  Unit connectToPort(PortI p){portTo = p;}
  Unit connectFromPort(PortI p){portFrom = p;}

  Unit filtered(Hashtag ht) {
    StreamM filter = Nil;
    await portFrom != null;
    StreamM st = await portFrom!receive(this);
    //println(`Filter has received $st$`);
    foreach (m in st) {
      foreach (h in hashtags(m)) {
        if (h == ht) {
          filter = appendright(filter,m);
          //println(`Filter has filter $m$`);
        }
      }
    }
    await portTo != null;
    portTo!send(this,filter);
    println(`Filter has sent $filter$`);
    this!filtered(ht);
   }
}


interface MapperI extends Tier {
  Unit mapped();
}

class Mapper() implements MapperI {
  PortI portFrom = null;
  PortI portTo = null;

  Unit connectToPort(PortI p){portTo = p;}
  Unit connectFromPort(PortI p){portFrom = p;}

  Unit mapped() {
    StreamM map = Nil;
    await portFrom != null;
    StreamM st = await portFrom!receive(this);
    foreach (m in st) {
      map = appendright(map,Author(authorId(m)));
    }
    await portTo != null;
    portTo!send(this,map);
    println(`Mapper has sent $map$`);
    this!mapped();
  }
}

interface FolderI extends Tier  {
  Unit folded();
  Unit printFolder();
}

class Folder() implements FolderI {
  PortI portFrom = null;
  Map<String,Int> folded = map[];

  Unit connectToPort(PortI p){skip;}
  Unit connectFromPort(PortI p){portFrom = p;}

  Unit folded() {
    await portFrom != null;
    StreamM st = await portFrom!receive(this);
    println(`==FOLDER HAS RECEIVED $st$==`);
    foreach (m in st) {
      folded = put(folded, authorId(m), lookupDefault(folded, authorId(m), 0)+1);
    }
    this!folded();
  }

  Unit printFolder(){
    foreach (e in  entries(folded)) {
      println(`$e$`);
    }
  }

}
