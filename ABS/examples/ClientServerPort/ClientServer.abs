/*************************************
Chat example, inspired by the scalalogi client - server chat example
FIRST VERSION BY(2021.04.24): S. Lizeth Tapia Tarifa
************************************/

module ClientServer;
export *;

import * from Medium;
import * from Event;
import * from Stream;
import * from Port;

interface ClientI extends Tier {
  Unit fire(String t);
  Unit listen();
  Unit print();
}

class Client(String name) implements ClientI {
  PortI portFrom = null;
  PortI portTo = null;
  List<String> chatlog = Nil;
  List<Tier> tiers = Nil;
  Int i = 0;

  Unit connectToTier(Tier t){tiers = appendright(tiers,t);}
  Unit connectToPort(PortI p){portTo = p;}
  Unit connectFromPort(PortI p){portFrom = p;}

  Unit fire(String t) {
    await portTo != null;
    await tiers != Nil;
    i = i+1;
    String message = ` $name$: [$i$] $t$ `;
    Event line = Line(this,message);
    chatlog = appendright(chatlog,message);
    foreach (tier in tiers){await portTo!fire(tier,line);}
  }

  Unit listen(){
    await portFrom != null;
    StreamM chat = await portFrom!receive(this);
    Int i = 0;
    Int size = length(chat);
    while (i<size) {
      chatlog = appendright(chatlog,msg(nth(chat,i)));
      i = i+1;
    }
    this!listen();
  }

  Unit print(){
    println(`Chat window for  $name$ ::: $chatlog$`);
  }
}


interface ServerI extends Tier {
  Unit broadcast();
  Unit print();
}

class Server(String name) implements ServerI {
  PortI portFrom = null;
  PortI portTo = null;
  List<String> chatlog = Nil;
  List<Tier> tiers = Nil;

  Unit connectToPort(PortI p){portTo = p;}
  Unit connectToTier(Tier t){tiers = appendright(tiers,t);}
  Unit connectFromPort(PortI p){portFrom = p;}

  Unit broadcast(){
    await portFrom != null;
    StreamM chat = await portFrom!receive(this);
    Int  size = length(chat);
    Int i = 0;
    while (i<size) {
      chatlog = appendright(chatlog,msg(nth(chat,i)));
      i = i+1;
    }
    await portTo!= null;
    await tiers != Nil;
    foreach (tier in tiers){
      i = 0;
      while (i<size) {
        if (authorId(nth(chat,i)) != tier) await portTo!fire(tier, nth(chat,i));
        i = i+1;
      }
    }
    this!broadcast();
  }

  Unit print(){
    println(`Chat window for  $name$ ::: $chatlog$`);
  }

}
