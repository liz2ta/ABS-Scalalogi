/*************************************
Stream implementation inspired by the places of scalalogi
FIRST VERSION BY(2021.04.24): S. Lizeth Tapia Tarifa
************************************/


module Stream;
import * from Event;
import * from Medium;
export *;

type StreamM = List<Event>;

interface InputStreamI {
  StreamM withdraw();
  Bool isActive();
}


class InputStream(MediumI c) implements InputStreamI {
  StreamM msgs = Nil;
  Bool active = False;

  Unit  deposit() {
      Event m = await c!receive();
      msgs = appendright(msgs, m);
      active = True;
  }

  StreamM withdraw() {
    await !isEmpty(msgs);
    StreamM tmp = msgs;
    msgs = Nil;
    active = False;
    return tmp;
  }

  Bool isActive(){return active;}

  Unit run(){
    await this!deposit();
    this!run();
  }
}

interface OutputStreamI {
  Unit deposit(StreamM st);
  Bool isActive();
}

class OutputStream(MediumI c) implements OutputStreamI {
  StreamM msgs = Nil;
  Bool active = False;

  Unit  deposit(StreamM st) {
      msgs = concatenate(msgs,st);
      active = True;
  }

  Unit withdraw() {
    await !isEmpty(msgs);
    Event h = head(msgs);
    msgs = tail(msgs);
    if (isEmpty(msgs)) active = False;
    await c!send(h);
  }

  Bool isActive(){return active;}

  Unit run(){
    await this!withdraw();
    this!run();
  }
}
