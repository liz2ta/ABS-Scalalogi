/*************************************
Stream implementation inspired by the places of scalalogy
FIRST VERSION BY(2021.04.24): S. Lizeth Tapia Tarifa
************************************/


module Stream;
import * from Event;
import * from Medium;
export *;

type StreamM = List<Event>;

interface InputStreamI {
  StreamM withdraw();
}


class InputStream(MediumI c) implements InputStreamI {
  StreamM msgs = Nil;

  Unit  deposit() {
      Event m = await c!receive();
      msgs = appendright(msgs, m);
  }

  StreamM withdraw() {
    await !isEmpty(msgs);
    StreamM tmp = msgs;
    msgs = Nil;
    return tmp;
  }

  Unit run(){
    await this!deposit();
    this!run();
  }
}

interface OutputStreamI {
  Unit deposit(StreamM st);
}

class OutputStream(MediumI c) implements OutputStreamI {
  StreamM msgs = Nil;

  Unit  deposit(StreamM st) {
      msgs = concatenate(msgs,st);
  }

  Unit withdraw() {
    await !isEmpty(msgs);
    Event h = head(msgs);
    msgs = tail(msgs);
    await c!send(h);
  }

  Unit run(){
    await this!withdraw();
    this!run();
  }
}
