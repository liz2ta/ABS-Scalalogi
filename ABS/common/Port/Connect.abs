/*************************************
Port connection implementation
FIRST VERSION BY(2021.04.24): S. Lizeth Tapia Tarifa
************************************/

module Connect;
export *;

import * from Medium;
import * from Event;
import * from Stream;
import * from Port;
//import * from EndPoint;


interface ConnectI {
  //manual stream
  Unit connect(Tier tierFrom, Tier tierTo, PortI port);
  Unit connectTo(Tier tierFrom, Tier tierTo, PortI port);
  //Unit connectToW(EndPointI tierFrom, Tier tierTo, PortI port);

  Unit oneManyConnect(Tier one, List<Tier> many, PortI port);
  Unit manyOneConnect(List<Tier> many, Tier one, PortI port);
  Unit oneManyConnectTo(Tier one, List<Tier> many, PortI port);
  Unit manyOneConnectTo(List<Tier> many, Tier one, PortI port);

  Unit manyManyConnect(List<Tier> manyFrom, List<Tier> manyTo, PortI port);
  Unit manyManyConnectTo(List<Tier> manyFrom, List<Tier> manyTo, PortI port);
}

class Connect() implements ConnectI {

  Unit connect(Tier tierFrom, Tier tierTo, PortI port){
    this.connectTo(tierFrom, tierTo, port);
    this.connectTo(tierTo, tierFrom, port);
  }

  Unit connectTo(Tier tierFrom, Tier tierTo, PortI port){
    await tierFrom!connectToPort(port);
    await tierTo!connectFromPort(port);
    await port!connectTo(tierFrom,tierTo,0);

  }

  /*Unit connectToW(EndPointI tierFrom, Tier tierTo, PortI port){
    await tierFrom!connectToWorker(port);
    await tierTo!connectFromPort(port);
    await port!connectTo(tierFrom,tierTo,0);

  }*/

  Unit oneManyConnect(Tier one, List<Tier> many, PortI port) {
    this.oneManyConnectTo(one, many, port);
    this.manyOneConnectTo(many, one, port);
  }

  Unit manyOneConnect(List<Tier> many, Tier one, PortI port){
    this.manyOneConnectTo(many, one, port);
    this.oneManyConnectTo(one, many, port);
  }

  Unit oneManyConnectTo(Tier one, List<Tier> many, PortI port){
    await one!connectToPort(port);
    foreach (tier in many){
        await tier!connectFromPort(port);
        await port!connectTo(one,tier,0);
    }
  }


  Unit manyOneConnectTo(List<Tier> many, Tier one, PortI port){
    foreach (tier in many){
        await tier!connectToPort(port);
        await port!connectTo(tier,one,0);
    }
    await one!connectFromPort(port);
  }


 Unit manyManyConnect(List<Tier> manyFrom, List<Tier> manyTo, PortI port){
    this.manyManyConnectTo(manyFrom, manyTo, port);
    this.manyManyConnectTo(manyTo, manyFrom, port);
 }

 Unit manyManyConnectTo(List<Tier> manyFrom, List<Tier> manyTo, PortI port){
    foreach (tier in manyFrom){
      this.oneManyConnectTo(tier, manyTo, port);
    }
  }
}
